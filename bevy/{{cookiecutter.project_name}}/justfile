# optional file for user for recipes that don't belong
# in version control
import? 'local.justfile'

flags := "--features=dev"
alias lint := clippy

export CARGO_TARGET_DIR := "target/just"

[private]
@default:
  just --list --justfile {{justfile()}}

# Build with dev features enabled
[no-exit-message]
build:
  cargo build {{flags}}

# Run with dev features enabled
[no-exit-message]
run *args:
  cargo run {{flags}} -- {{args}}

# Test with dev features enabled
[no-exit-message]
test:
  cargo nextest run {{flags}}

# Run cargo check with dev features enabled
[no-exit-message]
check *args:
  cargo check {{flags}} {{args}}

# Run clippy with dev features enabled
[no-exit-message]
clippy *args: (check args)
  cargo clippy {{flags}} {{args}}

# Build documentation with dev features enabled
[no-exit-message]
doc *args:
  cargo doc --all-features {{flags}} {{args}}

setup:
  pre-commit install --install-hooks -t pre-commit -t commit-msg
