# optional file for user for recipes that don't belong
# in version control
import? 'local.justfile'

flags := "--features=dev"
alias lint := clippy

[private]
@default:
  just --list --justfile {{justfile()}}

# Build with dev features enabled
[no-exit-message]
build:
  cargo build {{flags}}

# Run with dev features enabled
[no-exit-message]
run:
  cargo run {{flags}}

# Test with dev features enabled
[no-exit-message]
test:
  cargo nextest run {{flags}}

# Run clippy with dev features enabled
[no-exit-message]
clippy:
  cargo clippy {{flags}}

# Build documentation with dev features enabled
[no-exit-message]
doc:
  cargo doc {{flags}}
