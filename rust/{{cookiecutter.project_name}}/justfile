# optional file for user for recipes that don't belong
# in version control
import? 'local.justfile'

alias lint := clippy

export CARGO_TARGET_DIR := "target/just"

[private]
@default:
  just --list --justfile {{justfile()}}

# Build the project using cargo
[no-exit-message]
build:
  cargo build

# Test the project using cargo
[no-exit-message]
test *args:
  cargo nextest run {{args}}

# Run clippy on the project
[no-exit-message]
clippy *args: (check "{{args}}")
  cargo clippy {{args}}

# Build documentation using rustdoc
[no-exit-message]
doc *args:
  cargo doc --all-features {{args}}

setup:
  pre-commit install --install-hooks -t pre-commit -t commit-msg
